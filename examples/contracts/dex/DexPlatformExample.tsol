pragma ever-solidity >= 0.61.2;

pragma AbiHeader time;
pragma AbiHeader expire;
pragma AbiHeader pubkey;

import "@broxus/ever-contracts/contracts/libraries/MsgFlag.tsol";
import "@broxus/ever-contracts/contracts/libraries/UtilityGas.tsol";
import "@broxus/ever-contracts/contracts/reservation/abstract/Reservable.tsol";
import "@broxus/ever-contracts/contracts/reservation/abstract/TargetBalance.tsol";
import "@broxus/ever-contracts/contracts/validation/abstract/Validatable.tsol";

import "../../../contracts/dex/libraries/DexPlatformTypes.tsol";
import "../../../contracts/dex/DexPlatformMinimized.tsol";

contract DexPlatformExample is
    Reservable,
    Validatable,
    TargetBalance
{
    uint32 private static _nonce;

    function _getTargetBalanceInternal()
        internal
        view
        override
        returns (uint128)
    {
        return UtilityGas.INITIAL_BALANCE;
    }

    constructor(optional(address) _remainingGasTo)
        public
        reserveAcceptAndRefund(_getTargetBalanceInternal(), _remainingGasTo, msg.sender)
    {}

    function getPairAddress(
        TvmCell _platformCode,
        address _root,
        address _left,
        address _right
    )
        external
        pure
        responsible
        returns (address)
    {
        TvmBuilder builder;

        builder.store(_left);
        builder.store(_right);

        return {
            value: 0,
            flag: MsgFlag.REMAINING_GAS,
            bounce: false
        } address(
            tvm.hash(
                tvm.buildStateInit({
                    code: _platformCode,
                    contr: DexPlatformMinimized,
                    pubkey: 0,
                    varInit: {
                        root: _root,
                        type_id: DexPlatformTypes.POOL,
                        params: builder.toCell()
                    }
                })
            )
        );
    }
}
